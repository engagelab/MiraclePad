<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title.feed="LiveFeed" title.portfolio="Portfolio" creationComplete="initView(event)" destructionPolicy="auto">
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			import com.seesmic.as3.xmpp.JID;
			import com.seesmic.as3.xmpp.XMPP;
			import com.seesmic.as3.xmpp.XMPPEvent;
			
			import listeners.RetweetEvent;
			
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.utils.UIDUtil;
			
			import spark.components.CheckBox;
			import spark.core.NavigationUnit;
			
			private var _conn:XMPP;
			private var _currentGroup:Object;
			private var _hashEnergies:Array;
			private var _chosenEnergyString:String;
			
			[Bindable] public var tweetOwner:String;
			
			/**
			 * 
			 * init function for this view container
			 * 
			 **/
			protected function initView(event:FlexEvent):void {
				this.addEventListener(resourceManager.getString('resources', 'DISPATCH_REFRESH_TWEETS'), dispatchRefreshTweetHandler);
				this.addEventListener(resourceManager.getString('resources', 'RE_TWEET'), handleReTweet);
				
				_currentGroup = navigator.context.currentGroup;
				_hashEnergies = navigator.context.hashTags;
				_chosenEnergyString = navigator.context.chosenEnergyString;
				tweetOwner = new String(_currentGroup.title).toLowerCase();
				_conn = XMPP(data);
				_conn.addEventListener(XMPPEvent.MESSAGE_MUC, handleMUCMessage);
				_conn.plugin['muc'].joinRoom('conference.'+resourceManager.getString('resources', 'XMPP_SERVER'), resourceManager.getString('resources', 'TWEETS_ROOM_NAME'), new String(_currentGroup.title).toLowerCase()+"-"+UIDUtil.createUID());
				_conn.plugin['muc'].joinRoom('conference.'+resourceManager.getString('resources', 'XMPP_SERVER'), resourceManager.getString('resources', 'TWEETS_COMMUNICATION_ROOM_NAME'), new String(_currentGroup.title).toLowerCase()+"-"+UIDUtil.createUID());
			
				buildFilters();
			}
			
			/**
			 * 
			 * Event handling function for DISPATCH_REFRESH_TWEETS
			 * 
			 **/
			protected function dispatchRefreshTweetHandler(evt:Event):void {
				//using the message room to let all clients know
				_conn.plugin['muc'].sendMessage('conference.'+resourceManager.getString('resources', 'XMPP_SERVER'), resourceManager.getString('resources', 'TWEETS_COMMUNICATION_ROOM_NAME'), '{"userName":"'+new String(_currentGroup.title).toLowerCase()+'", "messageType":"'+resourceManager.getString('resources', 'TWEET_POST_SUCCESSFUL')+'"}', null);
			}
			
			/**
			 * 
			 * Event handling method for MUCMessages
			 * 
			 **/
			protected function handleMUCMessage(evt:XMPPEvent):void {
				if(!evt.stanza.body) {
					return;
				}
				
				var al:Number = 1;
				
				var xm:XML = evt.stanza.xml as XML;
				for each (var i:XML in xm.elements()) {
					if(i.name() == "urn:xmpp:delay::delay") {
						al = .5;
					}
				}
				
				switch(new String(evt.stanza.from.user)) {
					case resourceManager.getString('resources', 'TWEETS_ROOM_NAME'):
						var roomjid:JID = JID(evt.stanza.from);
						
						tweetFeedArray.addItem({bdy: evt.stanza.body, from: cleanResource(roomjid.getResource()), al: al, visible: true, tags: getTagsForContent(evt.stanza.body)});
						refreshFeed();
						break;
					
					case resourceManager.getString('resources', 'TWEETS_COMMUNICATION_ROOM_NAME'):
						var res:Object = com.adobe.serialization.json.JSON.decode(String(evt.stanza.body));
						//if(res.userName == new String(_currentGroup.title).toLowerCase() && res.messageType == resourceManager.getString('resources', 'TWEET_POST_SUCCESSFUL')) {
						if(res.tag == _chosenEnergyString && res.messageType == resourceManager.getString('resources', 'TWEET_POST_SUCCESSFUL') && al == 1) {
							loadTweets();
						}
						
						break;
				}
						
			}
			
			/**
			 * 
			 * Function to return a clean "from" user (without '-pad')
			 * 
			 **/
			protected function cleanResource(initialFrom:String):String {
				initialFrom = initialFrom.split("-")[0];
				return initialFrom;
			}
			
			/**
			 * 
			 * extract the tags from the tweet content
			 * 
			 **/
			protected function getTagsForContent(message:String):Array {
				var tags:Array = new Array();
				
				for each (var tag:String in _hashEnergies) {
					if(message.indexOf(tag) > -1) {
						tags.push(tag.substr(1));
					}
				}
				
				return tags;
			}
			
			/**
			 * 
			 * update the list of displayed tweets in the livefeed based on hash filters
			 * 
			 **/
			public function refreshFeed():void {
				var tweetsToDisplay:ArrayCollection = new ArrayCollection();
				for each (var tweet:Object in tweetFeedArray) {
					tweet.currentGroup = _currentGroup;
					if(hashTags.length > 0) {
						for each (var tag:String in hashTags) {
							if((tweet.tags as Array).indexOf(tag) > -1 && !tweetsToDisplay.contains(tweet)) {
								tweetsToDisplay.addItem(tweet);
							}
						}	
					}
					else {
						tweetsToDisplay.addItem(tweet);
					}
				}
				
				liveFeedList.dataProvider = tweetsToDisplay;
				scrollToBottom();
			}
			
			/**
			 * 
			 * public function to force scroll down on the list component
			 * 
			 **/
			protected function scrollToBottom():void {
				var delta:Number = 0;
				var count:int = 0;
				
				while (count++ < 10){
					liveFeedList.validateNow();
					delta = liveFeedList.layout.getVerticalScrollPositionDelta(NavigationUnit.END);
					liveFeedList.layout.verticalScrollPosition += delta;
					
					if (delta == 0) {
						break;						
					}
				}
			}
			
			/**
			 * 
			 * build dynamically the filters on creation complete
			 * 
			 **/
			protected function buildFilters():void {
				var cb:CheckBox;
				for each (var referenceHash:String in _hashEnergies) {
					cb = new CheckBox();
					filterGroup.addElement(cb);
					cb.label = referenceHash.substr(1);
					cb.setStyle("color", 0xFFFFFF);
					cb.addEventListener(Event.CHANGE, handleHashTag);
				}
			}
			
			/**
			 * 
			 * handles opening and closing the hash drawer
			 * 
			 **/
			protected function handleHashTag(event:Event):void {
				var tag:String = event.currentTarget.content;
				
				if(event.currentTarget.selected) {
					hashTags.addItem(tag);
				}
				else {
					hashTags.removeItemAt(hashTags.getItemIndex(tag));
				}
				
				refreshFeed();
			}
			
			/**
			 * 
			 * handles send tweet event
			 * 
			 **/
			protected function sendTweet():void {
				var params:Object = new Object();
				params.ownerName = tweetOwner;
				params.text = wordInput.text;				
				
				_conn.plugin['muc'].sendMessage('conference.'+resourceManager.getString('resources', 'XMPP_SERVER'), resourceManager.getString('resources', 'TWEETS_ROOM_NAME'), com.adobe.serialization.json.JSON.encode(params), "sciwork");
				wordInput.text = "";				
				tweetOwner = new String(_currentGroup.title).toLowerCase();
			}
			
			/**
			 * 
			 * Error handling method for getTweets service
			 * 
			 **/
			protected function getTweets_faultHandler(event:FaultEvent):void {
				trace("An error occured while retrieving the tweets from the server.");
			}
			
			/**
			 *
			 * Result handling method for getTweets service 
			 * 
			 **/
			protected function getTweets_resultHandler(event:ResultEvent):void {
				if(event.result != "") {
					var tweetsForGroup:Array = com.adobe.serialization.json.JSON.decode(String(event.result));
					var tempArray:Array = new Array();
					
					//build the correct arrays
					var tweet:Tweet;
					var tweetObject:Object;
					for each (var twt:Object in tweetsForGroup) {
						tweet = new Tweet();
						tweet.data = twt;
						
						//need to see if tweet is visisble
						if(twt.isVisible) {
							//place the tweets in the view
							//tweetsContainer.addElement(tweet);
							tempArray.push(tweet);
						}
					}
					
					if(this.currentState == "portfolio") {
						tweetsContainer.removeAllElements();
						for each (var visibleTweet:Tweet in tempArray) {
							tweetsContainer.addElement(visibleTweet);	
						}
					}
				}			
			}
			
			/**
			 *
			 * Function to call service to load tweets 
			 * 
			 **/
			protected function loadTweets():void {
				getTweets.url = resourceManager.getString('resources', 'TWEET_SERVER_URL') + "/tweet/" + _chosenEnergyString;
				getTweets.send();
			}
			
			/**
			 *
			 * Event handling method for RE_TWEET 
			 * 
			 **/
			protected function handleReTweet(evt:RetweetEvent):void {
				this.tweetOwner = evt.originGroup;
				wordInput.text = evt.tweetContent;
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:ArrayCollection id="tweetFeedArray" />
		<s:ArrayCollection id="hashTags" />
		<s:HTTPService method="GET" headers="{'pragma', 'no-cache'}" result="getTweets_resultHandler(event)" fault="getTweets_faultHandler(event)" resultFormat="text" id="getTweets" showBusyCursor="true" />
	</fx:Declarations>
	
	<s:states>
		<s:State name="feed" />
		<s:State name="portfolio" enterState="loadTweets()" />
	</s:states>
	
	<s:navigationContent>
		<s:Button includeIn="feed" label="Portfolio" click="this.currentState='portfolio'" />
		<s:Button includeIn="portfolio" label="LiveFeed" click="this.currentState='feed'"/>
	</s:navigationContent>
	
	<s:titleContent>
		<s:Label color="#FFFFFF" text="{this.title}" textAlign="center" width="100%" fontWeight="bold" />
	</s:titleContent>
	
	<s:actionContent>
		<s:Button label="Logout" click="dispatchEvent(new Event(resourceManager.getString('resources', 'LOGOUT'), true))" />
	</s:actionContent>
	
	<s:HGroup width="100%" height="100%" includeIn="feed">
		<s:BorderContainer width="250" height="100%" backgroundColor="#333333" skinClass="skins.hashPanelHeaderSkin">
			<s:VGroup width="100%" height="100%">
				<s:Label width="100%" paddingLeft="5" paddingRight="5" paddingTop="5" paddingBottom="5" text="Filters:" color="#FFFFFF" textDecoration="underline" />
				<s:VGroup id="filterGroup" width="100%" gap="15" paddingLeft="5" paddingRight="5" paddingTop="5" paddingBottom="5"/>
			</s:VGroup>
		</s:BorderContainer>
		<s:VGroup width="100%" height="100%">
			<s:List id="liveFeedList" width="100%" height="100%" selectionColor="#FFFFFF" horizontalScrollPolicy="off" borderVisible="false" itemRenderer="itemRenderers.TweetItem" >
				<s:layout>
					<s:VerticalLayout gap="2" horizontalAlign="contentJustify" variableRowHeight="true" />
				</s:layout>
			</s:List>
			<s:HGroup width="100%" gap="10" verticalAlign="middle" paddingBottom="10" paddingLeft="5" paddingRight="10">
				<s:TextInput id="wordInput" width="100%" height="{sender.height}" maxChars="140" />
				<s:Button id="sender" enabled="{wordInput.text.length > 0}" width="100" label="LEVER" chromeColor="{MiraclePad.getColorBasedOnGroup(new String(_currentGroup.title).toLowerCase())}" buttonMode="true" click="sendTweet()" />
			</s:HGroup>
		</s:VGroup>
	</s:HGroup>
	
	<s:Group width="100%" height="100%" includeIn="portfolio" id="tweetsContainer" />
</s:View>
